BeliefUpdates:
  { not assigned(POS, X) }  SetAssigned(POS, X)     { assigned(POS, X) }
  { true }  AddDeck(X) { deck(X) }
  { deck(X) }  RemoveDeck(X) { not deck(X) }
Beliefs:
  notary(n).
  gatekeeper(gk).
  player(X) :- assigned(POS, X).
  gethand([C1, C2, C3, C4, C5, C6, C7, C8, C9, C10, C11, C12, C13, C14, C15, C16 | NewDeck], [C1, C2, C3, C4, C5, C6, C7, C8, C9, C10, C11, C12, C13, C14, C15, C16], NewDeck).
  
Goals:
  deal(0)
  
Plans:
 @cardtable(enterAsDealer())
 
PG-rules:
  deal(To) <- deck(X) | 
  {
    /* send cards */ 
    B( gethand(X, H, T) );
    B( assigned(To, P0) );
    send(P0, inform, givecards(H));
    dropgoal(deal(To));
    RemoveDeck(X);
    AddDeck(T);
    if B( To < 4) then
    {
      adopta(deal(To + 1))
    }
    else
    {
      adopta(sendfirstbid(0))
    }
  }
  
  sendfirstbid(0) <- true | 
  {
    B( assigned(0, Next) ); 
    send(Next, inform, firstbid(0)); 
    dropgoal(sendfirstbid(0))
  }

PC-rules:
  message(X, inform, _, _, registered(POS, Y)) <- true |
  {
    SetAssigned(POS, Y)
  }

  message(X, inform, _, _, dealer(Y)) <- true |
  {
    /* here start shuffling and dealing cards */ 
    @cardtable(shuffleDeck())
  }  
  
  event(deckShuffled(X), cardtable) <- true | 
  {
    AddDeck(X)
  }
  
  
  